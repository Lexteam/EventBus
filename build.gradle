plugins {
    id 'java'

    id 'maven'
    id 'signing'

    id 'idea'
    id 'eclipse'

    id 'com.github.hierynomus.license' version '0.11.0'
}

defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'xyz.lexteam'
version = '1.5'
description = "EventBus - Somewhat performant event bus used in Lexteam projects."
ext.url = 'https://www.lexteam.xyz/'

repositories {
    mavenCentral()
}

dependencies {
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    include '**/*.groovy'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Lexteam Maven Repository
            repository(url: 'http://lex1.lexteam.xyz:8888/repository/internal/') {
                authentication(userName: lexRepoUser, password: lexRepoPass)
            }

            snapshotRepository(url: 'http://lex1.lexteam.xyz:8888/repository/snapshots/') {
                authentication(userName: lexRepoUser, password: lexRepoPass)
            }

            // Maven Central
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = 'https://github.com/Lexteam/EventBus'
                        connection = 'scm:git:https://github.com/Lexteam/EventBus.git'
                        developerConnection = 'scm:git:git@github.com:Lexteam/EventBus.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Lexteam/EventBus/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'jamierocks'
                            name 'Jamie Mansfield'
                            email 'dev@jamierocks.uk'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
